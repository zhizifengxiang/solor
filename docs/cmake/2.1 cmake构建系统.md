# 3 使用权限和作用域(build specification & usage requirement)
下面三个指令说明了文件和目录之间的依赖关系。

> 1，target_include_directories：指出目标文件所依赖的头文件目录。对应变量“INCLUDE_DIRECTORIES”，“INTERFACE_INCLUDE_DIRECTORIES”。  
2，target_compile_definitions：指出编译目标文件时需要用到的宏。对应变量“COMPILE_DEFINITIONS”和“INTERFACE_COMPILE_DEFINITIONS”.  
3，target_compile_options：指出编译时需要用到的选项，对应变量 “COMPILE_OPTIONS”和“INTERFACE_COMPILE_OPTIONS”.

上面三条指令都有PUBLIC, PRIVATE, INTERFACE三种作用域限定。每条指令接受若干个参数，每个参数都可用其中一种作用域限定修饰。如下：

```
target_compile_definitions (archive
  PRIVATE BUIDL_WITH_LZMA
  INTERFACE USING_ARCHIVE_LIB
  )
```

private、public、interface三个作用域限定修饰较难理解，下面翻译自Stack Overflow中的一个高票回答，其举例了一种比较特殊情况，本文将对三个修饰符进一步剖析。
>我们使用源文件a.cpp和头文件a.h编译库a。有三种情况使用#include引用头文件。  
(1)只在源文件a.cpp中使用#include（比如使用QtNetwork），由于头文件中没有用到QtNetwork的头文件，因此使用PRIVATE修饰。  
(2)只在头文件a.h中使用#include，这种情况只是接口依赖，所以使用INTERFACE。  
(3)在源文件a.cpp和头文件a.h中均使用了某个库的头文件，则使用PUBLIC修饰。

经过上面的解释，针对三个作用域修饰，我们可以得到以下几点了解：
>1 PRIVATE将依赖传递封锁，即所有依赖只能在本层使用，不能进一步向下传导。    
2 PUBLIC与PRIVATE完全相反，允许生成的目标携带这些依赖， 继续传导下去。  
3 INTERFACE指明了是接口依赖，而不是库的链接依赖，仅仅是提供声明和查询——头文件实际上只是声明和查询。

因此，由上面的解释，三种修饰需要遵循如下规则：
>1 PRIVATE不能修饰INTERFACE的变量，因为INTERFACE有可能需要暴露给后面的依赖关系。    
2 INTERFACE只能修饰interface类型的变量。

注意：作用域限定不是为了，给像COMPILE_OPTIONS和COMPILE_DEFINITIONS等变量传递参数，提供方便而设计的，变量值应该在当前层都能被用到。

## 1 目标属性 (target property)
下面三个变量用于控制目标编译过程。

> 1，INCLUDE_DIRECTORIES：存储头文件所在目录列表的变量。其对应命令行选项-I或者-isystem前缀。解析顺序和出现顺序一致。  
2，COMPILE_DEFINITIONS：存储宏定义列表的变量。其对应命令行选项-D或者/D开头的宏定义。注意，宏定义没有顺序性。库类型为SHARED或MODULE，使用变量DEFINE_SYMBOL来定义宏。  
3，COMPILE_OPTIONS：存储选项列表的变量。与shell中执行指令选项相同。许多编译选项会有特殊处理，如选项POSITION_INDEPENDENT_CODE。

上面三个变量加上INTERFACE_前缀后，成为INTERFACE类型变量。INTERFACE变量规定目标文件必须在编译和连接中被使用。如下所示：

```
set (src archive.cpp app.cpp)
if (LZMA_FOUND)
  list (APPEND src lzma.cpp) # 生成一个list, append指出如何生成
endif()

add_library (archive SHARED ${src})

if (LZMA_FOUND) # 库archive使用宏定义-DBUILDING_WITH_LZMA编译
  target_compile_definitions (archive PRIVATE DBUILDING_WITH_LZMA)
endif()

target_compile_definitions (archive INTERFACE USING_ARCHIVE_LIB)
add_executable (consumer)
# archive自带了INTERFACE宏定义USING_ARCHIVE_LIB,编译consumer时会加入宏定义-DUSING_ARCHIVE_LIB
target_link_libraries (consumer archive)
```


通常，源文件目录和目标文件目录，都会被加到”INCLUDE_DIRECTORIES“变量中（生成的头文件会放在目标文件目录），方便起见，将变量CMAKE_INCLUDE_CURRENT_DIR=ON，可以直接添加两个目录，供所有目标文件访问。

CMAKE_INCLUDE_CURRENT_DIR_IN_INTERFACE=ON，会将所有的目标文件对应的目录，都添加到INTERFACE_INCLUDE_DIRECTORIES中。方便target_link_libraries链接，具有多个目标的项目——因为INTERFACE要求必须所有目标都要链接。


## 2 目标传递使用要求 (transitive usage requirement)

对于作用域限定符的文件、变量，其可与生成的目标(target)的依赖关系进行传递。我们在指令“target_link_libraries”中，使用PUBLIC, PRIVATE, INTERFACE修饰，。

如下指令，archive和serialization两个目标分别定义了INTERFACE类型的宏，因此，凡是依赖于这两个的目标的文件，都必须继承并使用定义的宏。

archiveExtras公有依赖于archive,因此，依赖于archiveExtras的文件也继承了archive的宏。
archiveExtras私有依赖于serialization,因此，依赖于archiveExtras的文件无法继承serialization的宏。

```
add_library (archive archive.cpp)
target_compile_definitions (archive INTERFACE USING_ARCHIVE_LIB) # INTERFACE指出必须使用该定义的宏

add_library (serialization serialization.cpp)
target_compile_definitions (serialization INTERFACE USING_SERIALIZATION_LIBS)

# 第一次依赖传递
# 传入archiveExtras的宏定义编译选项：-DUSING_ARCHIVE_LIB和-DUSING_SERIALIZATION_LIB
add_library (archiveExtras extras.cpp)
target_link_libraries (archiveExtras PUBLIC archive)
target_link_libraries (archiveExtras PRIVATE serialization) #private禁止向上传递

# consumer只使用编译选项：-DUSING_ARCHIVE_LIB
add_executable (consumer consumer.cpp)
target_link_libraries (consumer archiveExtras)
```

注意：
> 1，限定符private通常只用于库依赖关系，比如库文件b依赖于库a，可执行文件c依赖于库b。那么c不需要知道a中的具体内容，其只需要知道b中的内容，然后与b进行链接操作即可。  
>
>但是头文件不能采用private依赖，因为头文件有可能跨越多层使用。比如库b依赖于头文件a，库c既依赖于库b，也依赖头文件a，需要a中提供的借口。
>
> 2，若库和其头文件都依赖于某个头文件，则其为public。若只有头文件依赖于某个头文件，则使用interface。

在变量传递过程中，构建系统会读取依赖关系中的INTERFACE类型变量，并将其值放到非-INTERFACE变量类型中。比如，INTERFACE_INCLUDE_DIRECTORIES中的值，会被放到INCLUDE_DIRECTORIES中。

若连接顺序有要求，那么最好就直接重新设置变量顺序。如下例子，库的连接顺序为lib1, lib2, lib3，但是目录的保函顺序必须为lib3, lib1, lib2，则命令如下：

```
target_link_libraries (myExe lib1 lib2 lib3)
target_include_directories (myExe PRIVATE $<TARGET_PROPERTY:lib3, INTERFACE_INCLUDE_DIRECTORIES>)
```
